set term wxt 1
set xr [0:300]
set yr [-300:0]

set size square

set style line 2 lt 3 lw 2

set style arrow 1 head filled size screen 0.025,10,40 linecolor rgb "black" linewidth 2
set style arrow 4 head filled size screen 0.03,10 ls 2 linecolor rgb "black" linewidth 2

flePnts = 'pnts.dat'
fleEdges = 'edges_vectors.dat'

loadEdges = sprintf('< gawk '' \
    FNR==NR{x[$1]=$2;y[$1]=$3;next;} \
    {printf "%%f\t%%f\t%%f\t%%f\n\n", x[$1], y[$1], (x[$2]-x[$1]), (y[$2]-y[$1]);} \
'' %s %s', flePnts, fleEdges);

loadWeights = sprintf('< gawk '' \
    FNR==NR{x[$1]=$2;y[$1]=$3;next;} \
    {printf "%%f\t%%f\t%%s\n", (x[$1]+x[$2])/2 + $4, (y[$1]+y[$2])/2 + $5, $3} \
'' %s %s', flePnts, fleEdges);

plot \
    loadEdges using 1:2:3:4 with vectors arrowstyle 4  notitle, \
    flePnts using 2:3:(6.0) with circles  linecolor rgb "white" lw 2 fill solid border lc lt 0 notitle, \
    flePnts using 2:3:1 with labels tc rgb "black" font "Arial Bold" notitle, \
    loadWeights using 1:2:3 with labels tc rgb "red" center font "Arial Bold" notitle
